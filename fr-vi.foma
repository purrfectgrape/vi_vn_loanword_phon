# FRENCH SOUND INVENTORY
# The French vowels system consists of 12 oral vowels and 4 nasal vowels
define frOralVowels [ i | y | e | ø | ɛ | œ | ə | a | u | o | ɔ | ɑ ];
define frNasalVowels [ ɛ̃ | œ̃ | ɔ̃ | ɑ̃ ];

# Set of French consonants
define frCons [ p | b | t | d | k | g | m | n | ɲ | ŋ | f | v | s | z | ʃ | ʒ | ʁ |  w | l | j | ɥ ];
define frLabials [ p | b | f | m ];
define frDentals [ t | d | s | z | n | l ];
define frPalatals [ ɲ ];
define frNasals [ m | n | ɲ | ŋ ];
define frVelars [ k | g ];
define frSounds [frOralVowels | frNasalVowels | frCons];

# VIETNAMESE SOUND INVENTORY

# Set of Vietnamese vowels
define viVowels [ i | e | ɛ | ɤ | ɤ̆ | a | ă | u | o | ɔ | ɯ | "iə"  | "uə" | "ɯə" ];
# Note that some analysis uses c instead of t͡ɕ. We use the same convention as in Kirby's paper and use t͡ɕ for Vietnamese palatal.
define viCons [ p | ɓ | t | tʰ | ɗ | t͡ɕ | k | ʔ |  m | n | ɲ | ŋ | f | v | s | z | x | ɣ | h | w | j | l | r ];
define viSounds [ viVowels | viCons ];

# PHONOLOGICAL ADAPTATION PATTERN RULES

# CONSONANT MAPPINGS

# Context-free mapping for consonants.
define bChange [ b -> ɓ ];
define pChange [ p -> ɓ || .#. _ , "." _ ]; # onset only.
define dChange [ d -> ɗ ];
define gChange [ g -> ɣ ];
define trChange [ t ʁ -> t͡ɕ ];  # Due to Hanoi dialect.
define rChange [ ʁ -> z ]; # Also Hanoi dialect. Removed mapping to r from gold test set.
#define njChange [ n j -> ɲ ];
define postAlveolarChange [ ʃ -> s , ʒ -> z ];
define contextFreeMappings [ pChange .o. bChange .o. dChange .o. gChange .o. trChange .o. rChange .o. postAlveolarChange ];

# Gemination.
#define eGem [ "." f -> f "." f , "." s -> s "." s , "." t -> t "." t , "." l -> l "." l , "." n -> n "." n || e _ , a _ ];
#define oGem [ "." n -> n "." n , "." m ->  m "." m || o  _ ];
#define Gemination [ eGem .o. oGem ];
 
# ONSET CONSONANT RULES
# French onset consonant clusters include: sp, st, sk, spl, spʁ, stʁ, skl, skʁ,
# pn, ps, pf, pt, kn, km, kv, ks, kt,
# tl, tm, ts, tʃ, sl, sm, sn, sv, sf, ʃl, ʃʁ, ʃn, ʃv, ʃpʁ, psk, gn, gz, dz, dʒ, zl, zv, zb, zgʁ, mn, ft.

# Epenthesis rules
define jRule [ j -> z || .#. _ , "." _ ,,
	       [ m | n ] j -> ɲ || _ frOralVowels ,
	       j -> i "." || [ m | n ] _ frOralVowels ];  # Conflict with rule n j above?
define epenthesisOnset [ b l -> b ɤ "2" "." l ,
			 g l -> g ɤ "2" "." l ,
                         g l -> g ,
			 g ʁ -> g ɤ "2" "." ʁ ,
			 k l -> k ɤ "2" "." l ,
			 s l -> s i "2" "." l ,
  			 s t -> s i "2" "." t ,
			 s k -> s i "2" "." k ,
			 s p -> s i "2" "." b || .#. _ , "." _ ];

# Deletion rules.
define deletionOnset [ k ʁ -> k ,,
		       b ʁ -> ɓ ,,
		       f ʁ -> f ,,
                       p l -> p ,,
                       f l -> f || .#. _ , "." _ ]; 
define onsetMappings [ epenthesisOnset .o. jRule .o. deletionOnset ];

# CODA CONSONANT RULES

define rCodaChange [ ʁ -> k || _ "." ,,  # ʁ is changed to k word-medially
		     ʁ -> 0 || _ .#. ]; # ʁ is deleted word finally, as observed in Kang et al.
define labialCodaChange [ [ frLabials - m ] -> p || _ .#. , _ "."];
define dentalCodaChange [ [ frDentals - [ n | l ] ] -> t || _ .#. , _ "." ];
define velarCodaChange [ [ k | g ] -> k || _ .#. , _ "."];
define lCodaChange [ l -> n ||  _ .#. , _ "."  ];
define postAlveolarCodaChange [ [ ʃ | ʒ ] -> t , ʃ -> k || _ .#. , _ "."];

define codaCluster [ l m -> m ,
		     l n -> m ,
                     l k -> n ,
                     t k -> t ,
		     k t -> k ,
                     k s -> k ,
                     k p -> k , # this is applied after rCodaChange
                     k n -> k ,
                     ŋ t -> ŋ ,
		     m ɓ -> m ,
                     m p -> m || _ .#. , _ "." ];

define codaMappings [ rCodaChange .o. labialCodaChange .o. dentalCodaChange .o. velarCodaChange .o. lCodaChange .o. postAlveolarCodaChange .o. epenthesisOnset .o. codaCluster ];

# VOWEL MAPPINGS. Implemented based on descriptions from Kang's paper.
define yToU [ y -> u || ʁ _ ];
define yToI [ y -> i || frVelars _ ];
define yContextFree [ y -> w i ]; # The most accurate mapping is to "wi".
define schwaContextFree [ [ ø | œ | ə ] -> ɤ ];
#define oContextFree [ [ o | ɔ ]-> [ o | ɔ ] ];
#define eContextFree [ [ e | ɛ ] -> [ e | ɛ ] ];
define aChange [ a -> ă || _ [ frPalatals | frVelars ] , j _]; # a remains a before uvular r.  
define dipthongsforR [ 	u -> "uə" ,
 			i -> "iə" ,
			y ->  u || _ ʁ ];
define loiDePositionbeforeR [ ɛ -> ɛ , ɔ -> ɔ || _ ʁ ]; 
define loiDePosition [ 	ɔ -> o , ɛ -> e || _ "." , _ .#. ,,
                        o -> ɔ , e -> ɛ || _ viCons ];  # o and e are preferred in open syllables, ɔ and ɛ in closed.
define nasalVowelsBeforeLabials [ ɛ̃ -> ɛ m ,
				  ɑ̃ -> ă m ,
 				  ɔ̃ -> [ o | ɔ ]  m || _ (".") [p | b] ];
define nasalVowelsContextFree [ ɛ̃ -> ɛ ŋ ,
		     ɑ̃ -> ă ŋ , 
		     ɔ̃ -> o ŋ ];  # do not include ɔ̃ -> ɔ ŋ because of how rare it is.
define vowelMappings [ loiDePositionbeforeR .o. loiDePosition .o. aChange .o. dipthongsforR .o. nasalVowelsBeforeLabials .o. nasalVowelsContextFree .o. yToU .o. yToI .o.  yContextFree .o. schwaContextFree ];

# TONE ASSIGNMENT (applied at the end)
define RisingTone [ [..] -> 5 || [p | t | k] _ .#. , [p | t | k] _ "." ];
define defaultToHighLevel [ [..] -> "1" || viSounds _ .#. , viSounds _ "." ]; 
define toneChange [ RisingTone .o. defaultToHighLevel ];
define insertGlottal [ [..] -> ʔ || .#. _ viVowels ];

# Rule composition
# VowelMappings need to be applied before codaMappings because before example loiDePositionbeforeR has to be applied before rCodaChange is deleted or changed to k.

define FrenchToVietnamese [ onsetMappings .o. vowelMappings .o. codaMappings .o. contextFreeMappings .o. toneChange .o. insertGlottal ];
re FrenchToVietnamese;
